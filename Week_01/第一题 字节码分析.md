###JAVA代码
```$xslt 
        boolean bo = true;
        byte    b  = 15;
        short   s  = 233;
        char    c  = 'e';
        int     i  = 10;
        long    l  = 11111L;
        float   f  = 1124.123F;
        double  d  = 123.213;

        byte n = (byte) (c + i);

        int plus     = 10 + 2;
        int minus    = 20 - 1;
        int multiply = 3 * 20;
        int divide   = 17 / 2;

        if (10 + 2 == 12) {
            System.out.println("hello if");
        } else {
            System.out.println("hello else");
        }

        System.out.println("----------------");
        int[] a = new int[]{1, 3, 9};
        for (int i1 : a) {
            System.out.println(i1);
        }
        System.out.println("------------------");
        for (int j = 0; j < 10; j++) {
            System.out.println(j);
        }

```

###字节码

```$xslt

  public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      # 栈深度为4 共有21个本地变量 方法入参为1个
      stack=4, locals=21, args_size=1
      #将1push到操作栈
         0: iconst_1 
      #将1弹出赋值给1号槽位 bo
         1: istore_1
      #将15push到操作栈
         2: bipush        15
      #将15弹出栈赋值给本地变量表2号槽位 b
         4: istore_2
      #将233push到操作栈
         5: sipush        233
      #将233pop存储到本地变量表3号槽位 s
         8: istore_3
      #将101push到操作栈
         9: bipush        101
      #pop 101 存储到本地变量表4号槽位 c
        11: istore        4
      #将10 push到操作栈上
        13: bipush        10
      #存储在5号槽位 i
        15: istore        5
      #从常量池load long型数据 11111l 到操作栈
        17: ldc2_w        #2                  // long 11111l
      # 将long 11111l存储到6号7号槽位
        20: lstore        6
      #将float 1124.123f 从常量池load到操作栈
        22: ldc           #4                  // float 1124.123f
      #将float 1124.123f 存储到8号槽位
        24: fstore        8
      #将123.213d load到操作栈
        26: ldc2_w        #5                  // double 123.213d
      #存储到9号10号槽位
        29: dstore        9
      # char load到栈上
        31: iload         4
      # int load到栈上
        33: iload         5
      # 相加得到结果放在栈上
        35: iadd
      # 强转成byte
        36: i2b
      # 存储到11号槽位
        37: istore        11
      # 将12推到栈上 这里编译器直接算出结果 plus = 10 + 2
        39: bipush        12
       # 赋值给12槽位
        41: istore        12
       # 20-1 直接编译器计算 推到栈上，不涉及变量，编译器会进行优化
        43: bipush        19
       # 赋值给13槽位
        45: istore        13
        # 3 * 20的结果同样编译器直接算出来
        47: bipush        60
        # 存储在14号槽位
        49: istore        14
       # 17/2又是编译器算好了
        51: bipush        8
       # 存储在15号槽位
        53: istore        15
       # 获取System.out对象
        55: getstatic     #7                  // Field java/lang/System.out:Ljava/io/PrintStream;
       # load 到常量池 "hello if" 
        58: ldc           #8                  // String hello if
      # 调用println方法
        60: invokevirtual #9                  // Method java/io/PrintStream.println:(Ljava/lang/String;)V
     
      # 获取System.out对象 打印flag分割线
        63: getstatic     #7                  // Field java/lang/System.out:Ljava/io/PrintStream;
        66: ldc           #10                 // String ----------------
        68: invokevirtual #9                  // Method java/io/PrintStream.println:(Ljava/lang/String;)V
      # 数组长度是3 编译器优化直接算出来
        71: iconst_3
      # 新建array对象 
        72: newarray       int
      # 复制一份
        74: dup
      # 栈上放0
        75: iconst_0
      # 栈上放1
        76: iconst_1
      # 数组存放arr[0]=1
        77: iastore
        78: dup
      
      # 数组存放arr[1]=3
        79: iconst_1
        80: iconst_3
        81: iastore
        82: dup
      
      # 数组存放arr[2]=9
        83: iconst_2
        84: bipush        9
        86: iastore
      # 数组放在16号槽位 a
        87: astore        16
      # load 数组
        89: aload         16
      # 存在 17号槽位
        91: astore        17
      # load 数组
        93: aload         17
      # 求数组大小
        95: arraylength
      # 存储在18号槽位
        96: istore        18
      # load 0 
        98: iconst_0
      # 存在19号槽位
        99: istore        19
      # load 19 号槽位的0
       101: iload         19
      # 18号槽位的数组大小
       103: iload         18
      # 比较是否大于等于，如果大于等于跳出循环 
       105: if_icmpge     129
      # load出数组
       108: aload         17
      # index
       110: iload         19
      # 获取数组中的值
       112: iaload
      # 存在20槽位
       113: istore        20
       
      # 打印20槽位
       115: getstatic     #7                  // Field java/lang/System.out:Ljava/io/PrintStream;
       118: iload         20
       120: invokevirtual #11                 // Method java/io/PrintStream.println:(I)V
      # 索引槽位++
       123: iinc          19, 1
      # 继续循环
       126: goto          101
     
     # 打印分割线
       129: getstatic     #7                  // Field java/lang/System.out:Ljava/io/PrintStream;
       132: ldc           #12                 // String ------------------
       134: invokevirtual #9                  // Method java/io/PrintStream.println:(Ljava/lang/String;)V
      
      # j = 0
       137: iconst_0
       138: istore        17
       140: iload         17
      # 总大小为10
       142: bipush        10
      #如果大于等于跳出循环
       144: if_icmpge     161
      #打印j
       147: getstatic     #7                  // Field java/lang/System.out:Ljava/io/PrintStream;
       150: iload         17
       152: invokevirtual #11                 // Method java/io/PrintStream.println:(I)V
      # j++
       155: iinc          17, 1
      # 继续循环
       158: goto          140
       161: return


      LocalVariableTable:
        Start  Length  Slot  Name   Signature
          115       8    20    i1   I
          140      21    17     j   I
            0     162     0  args   [Ljava/lang/String;
            2     160     1    bo   Z
            5     157     2     b   B
            9     153     3     s   S
           13     149     4     c   C
           17     145     5     i   I
           22     140     6     l   J
           26     136     8     f   F
           31     131     9     d   D
           39     123    11     n   B
           43     119    12  plus   I
           47     115    13 minus   I
           51     111    14 multiply   I
           55     107    15 divide   I
           89      73    16     a   [I

```

